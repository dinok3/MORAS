class Main{
    function void main(){
        var Array A,i;
        let A = Array.new(7);
        let A[0] = 6;
        let A[1] = 32;
        let A[2] = 0;
        let A[3] = 4;
        let A[4] = 3;
        let A[5] = 7;
        let A[6] = 2;

        do Main.QuickSort(A, 0, 6);
        do Output.println();
        let i = 0;
        while(i < 7){
            do Output.printInt(A[i]);
            do Output.println();
            let i = i + 1;
        }
    
        do A.dispose();
        return;
    }

    function void QuickSort(Array A, int l, int r){
        var int pivot;
        
        
        if((l = r) | (l > r)){
            return; 
        }

        
        let pivot = Main.partition(A, l, r);

        do Main.QuickSort(A, l, pivot - 1);
        do Main.QuickSort(A, pivot + 1, r);
        return;
    }

    function int partition(Array A, int l, int r){
        var int pivot_i,i,j, pivot;
        
        let pivot_i = l;
        let pivot = A[l]; 
        let j = l + 1;
        while((j<r) | (j=r)){
            if((A[j] < pivot) | (A[j] = pivot)){
                let pivot_i = pivot_i + 1;
                let i = A[j];
                let A[j] = A[pivot_i]; 
                let A[pivot_i] = i;
                
            }    
            let j = j + 1;
        }

        
        let i = A[pivot_i];
        let A[pivot_i] = pivot;
        let A[l] = i;
        return pivot_i;
    }
}